<!-- 
// *******************************************************
// CS 174a Graphics Example Code
index.html - The web page document containing the canvas (drawing surface), that launches your javascript files, and hides the vertex and fragment shader programs in strings. //-->
<!DOCTYPE html>
<html>
<head>
<script id="vertex-shader" type="x-shader/x-vertex">
// *******************************************************
// The following string is loaded by our javascript and then used as the Vertex Shader program.  Our javascript sends this code to the graphics card at runtime, where on each run it gets 
// compiled and linked there.  Thereafter, all of your calls to draw shapes will launch the vertex shader program once per vertex in the shape (three times per triangle), sending results on 
// to the next phase.  The purpose of this program is to calculate the final resting place of vertices in screen coordinates; each of them starts out in local object coordinates.

precision mediump float;
const int N_LIGHTS = 2;

attribute vec4 vColor;
attribute vec3 vPosition, vNormal;
attribute vec2 vTexCoord;
varying vec2 fTexCoord;
varying vec3 N, E, pos;

uniform float ambient, diffusivity, shininess, smoothness, attenuation_factor[N_LIGHTS];
uniform bool SOLID, GOURAUD, COLOR_NORMALS, COLOR_VERTICES;		// Flags for alternate shading methods

uniform vec4 lightPosition[N_LIGHTS], lightColor[N_LIGHTS], color, SOLID_COLOR;
varying vec4 VERTEX_COLOR;
varying vec3 L[N_LIGHTS], H[N_LIGHTS];
varying float dist[N_LIGHTS];

uniform mat4 camera_transform, camera_model_transform, projection_camera_model_transform;
uniform mat3 camera_model_transform_normal;

void main()
{
    N = normalize( camera_model_transform_normal * vNormal );
	
	vec4 object_space_pos = vec4(vPosition, 1.0);
    gl_Position = projection_camera_model_transform * object_space_pos;

	if( SOLID || COLOR_NORMALS || COLOR_VERTICES )		// Bypass phong lighting if we're lighting up vertices some other way
	{
		VERTEX_COLOR   = SOLID ? SOLID_COLOR : ( COLOR_NORMALS ? abs( vec4( N, 1.0 ) ) : vColor );
		VERTEX_COLOR.a = VERTEX_COLOR.w;
		return;
	}

    pos = ( camera_model_transform * object_space_pos ).xyz;
	E = normalize(-pos); 
	
	for( int i = 0; i < N_LIGHTS; i++ )
	{
		L[i] = normalize( ( camera_transform * lightPosition[i] ).xyz - lightPosition[i].w * pos );		// Use w = 0 for a directional light -- a vector instead of a point.   
		H[i] = normalize( L[i] + E );
		
		dist[i]  = distance((camera_transform * lightPosition[i]).xyz, pos);
	}

	if( GOURAUD )
	{
		VERTEX_COLOR = vec4( color.xyz * ambient, color.w);
		for(int i = 0; i < N_LIGHTS; i++)
		{
			float attenuation = 1.0 / (1.0 + attenuation_factor[i] * (dist[i] * dist[i])); 
			float diffuse  = max( dot(L[i], N), 0.0 );
			float specular = pow( max(dot(N, H[i]), 0.0), smoothness );

			VERTEX_COLOR.xyz += attenuation * ( color.xyz * diffusivity * diffuse + lightColor[i].xyz * shininess * specular );
		}
		VERTEX_COLOR.a = VERTEX_COLOR.w;
	}  
	fTexCoord = vTexCoord;  
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
// *******************************************************
// Likewise, the following string is loaded by our javascript and then used as the Fragment Shader program, which gets sent to the graphics card at runtime.  The fragment shader runs 
// once all vertices in a triangle / element finish their vertex shader programs, and thus have finished finding out where they land on the screen.  The fragment shader fills in (shades)
// every pixel (fragment) overlapping where the triangle landed.  At each pixel it interpolates different values from the three extreme points of the triangle, and uses them in formulas
// to determine color.

precision mediump float;

const int N_LIGHTS = 2;

uniform vec4 lightColor[N_LIGHTS], color;
varying vec3 L[N_LIGHTS], H[N_LIGHTS];
varying float dist[N_LIGHTS];
varying vec4 VERTEX_COLOR;

uniform float ambient, diffusivity, shininess, smoothness, animation_time, attenuation_factor[N_LIGHTS];

varying vec2 fTexCoord;		// per-fragment interpolated values from the vertex shader
varying vec3 N, E, pos;

uniform sampler2D texture; 
uniform bool SOLID, GOURAUD, COLOR_NORMALS, COLOR_VERTICES, USE_TEXTURE;

void main()
{    
	if( SOLID || GOURAUD || COLOR_NORMALS )		// Bypass phong lighting if we're only interpolating predefined colors across vertices
	{
		gl_FragColor = VERTEX_COLOR;
		return;
	}
	
	vec4 tex_color = texture2D( texture, fTexCoord );
	gl_FragColor = tex_color * (USE_TEXTURE ? ambient : 0.0 ) + vec4(color.xyz * ambient, USE_TEXTURE ? color.w * tex_color.w : color.w ) ;
	for( int i = 0; i < N_LIGHTS; i++ )
	{
		float attenuation = 1.0 / (1.0 + attenuation_factor[i] * (dist[i] * dist[i])); 
		float diffuse  = max( dot(L[i], N), 0.0 );
		float specular = pow( max(dot(N, H[i]), 0.0), smoothness );
	
		gl_FragColor.xyz += attenuation * (color.xyz * diffusivity * diffuse  + lightColor[i].xyz * shininess * specular );
	}
	gl_FragColor.a = gl_FragColor.w;
}
</script>

<!-- 
// *******************************************************
Javascript "include" files.  Any code in them gets executed as part of the page loading.
//-->


<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/underscore.js"></script>
<script type="text/javascript" src="../Common/shortcut.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="../Common/shape.js"></script>
<script type="text/javascript" src="../Common/webgl-obj-loader.js"></script>
<script type="text/javascript" src="../Common/GL_Context.js"></script>
<script type="text/javascript" src="shapes.js"></script>
<script type="text/javascript" src="animation.js"></script>

</head><body>
<canvas id="gl-canvas" width="940" height="560"></canvas><p>
<audio autoplay loop style="visibility: hidden;">
    <source src="super_mario_bros.mp3" type="audio/mp3">
    Your browser does not support the audio element.
</audio>

<footer>
	CS 174A HW2 | Spring 2016<br/>
	Copyright &copy; 2016 Matthew Allen Lin. All rights reserved. <br/>
	Audio: Super Mario Bros. Theme by Nintendo
</footer>

Controlling this Graphics Canvas: 
<ul><li>
		There are three modes:
		<ol>
		<li> Camera mode: controlled by w (up), a (left), s (down), d (right), and you can rotate the camera with f.
		<li> Ball mode: ALT + a to activate. This allows you to control the ball's x and z movement with the arrow keys. You can move up by pressing c. Hitting ALT + c turns on gravity, but there is a bug where you cannot move with the arrow keys or else your ball matrix will go off the game scene.
		<li> Hamster pan mode: ALT + x to activate. Pans the camera on the hamster.
		</ol>
<li> To check camera shading, use ALT + n.
<li> Requirements fulfilled: 
	<ol>
		<li>Two-level hierarchical object: sunflower
		<li>lookAt: ball and hamster
		<li>Custom shapes: sunflower head, cheese
		<li>Flat shading: cheese
		<li>Texture: ball
	</ol>
</p></body></html>
